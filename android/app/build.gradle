plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
}

android {
    namespace "com.aloe.android"
    compileSdk 34
    buildToolsVersion "34.0.0"
    signingConfigs {
        sign {
            Properties properties = new Properties()
            InputStream inputStream = project.rootProject.file('local.properties').newDataInputStream()
            properties.load(inputStream)

            storeFile file(properties.getProperty('key.file'))
            storePassword properties.getProperty('key.store')
            keyAlias properties.getProperty('key.alias')
            keyPassword properties.getProperty('key.pwd')
        }
    }
    defaultConfig {
        applicationId "com.aloe.android"
        minSdk 26
        targetSdk 34
        versionCode 1
        versionName "1.0"
        resConfigs "zh"//,"en"
        vectorDrawables { useSupportLibrary true }
        //noinspection ChromeOsAbiSupport
        ndk { abiFilters "arm64-v8a" }
        signingConfig signingConfigs.sign
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        profile {
            initWith debug
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.4.7'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    flavorDimensions "env", "store"
    productFlavors {
        mini { dimension "env" }
        full { dimension "env" }
        huawei { dimension "store" }
        xiaomi { dimension "store" }
    }
}

dependencies {
    implementation('androidx.webkit:webkit:1.7.0')
    implementation platform('androidx.compose:compose-bom:2023.06.00')
    implementation('androidx.compose.material3:material3')
    implementation('androidx.navigation:navigation-compose:2.6.0')
    implementation project(path: ':flutter')
//    debugImplementation 'com.aloe.flu:flutter_debug:1.0'
//    profileImplementation 'com.aloe.flu:flutter_profile:1.0'
//    releaseImplementation 'com.aloe.flu:flutter_release:1.0'
    debugImplementation('androidx.compose.ui:ui-tooling')
    debugImplementation('androidx.compose.ui:ui-tooling-preview')
    testImplementation('junit:junit:4.13.2')
}